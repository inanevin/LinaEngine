/*
This file is a part of: Lina Engine
https://github.com/inanevin/LinaEngine

Author: Inan Evin
http://www.inanevin.com

Copyright (c) [2018-] [Inan Evin]

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
*/

#lina_vs

layout (location = 0) in vec2 pos;
layout (location = 1) in vec2 uv;
layout (location = 2) in vec4 col;

layout (location = 0) out vec4 outCol;
layout (location = 1) out vec2 outUV;

void main()
{
   outCol = col;
   outUV = uv;
   gl_Position = LINA_VIEW.proj * vec4(pos.x, pos.y, 0.0f, 1.0);
}

#lina_end

#lina_fs

layout (location = 0) in vec4 inCol;
layout (location = 1) in vec2 inUV;

layout (location = 0) out vec4 fragColor;


layout(set = 2, binding = 0) uniform MaterialData
{
   vec4 color1;
   vec4 color2;
   vec4 floatPack1;
   vec4 floatPack2;
} material;

layout (set = 2, binding = 1) uniform mediump sampler guiSampler;
layout (set = 2, binding = 2) uniform mediump texture2D guiTexture;

// BufferType 0: Default
// BufferType 1: Gradient
// BufferType 2: Textured
// BufferType 3: Text
// BufferType 4: SDF Text

void main()
{
   fragColor = inCol;
   highp int bufferType = int(material.floatPack2.a);
   if(bufferType == 1)
   {
      // floatPack1 = uvMultiply, uvShift
      // floatPack2 = radialInfluence, isAABuffer, radialSize, bufferType

      // Linear gradient
      float t1 = inUV.x * material.floatPack1.r + inUV.y * material.floatPack1.g;

      // Radial gradient
      float t2 = length((inUV - material.floatPack1.ba) * material.floatPack2.b);

      float t = t1 * (1.0f - material.floatPack2.r) + t2 * (material.floatPack2.r);

      vec4 col = mix(material.color1, material.color2, t);
      float alpha = inCol.a * material.floatPack2.g + col.a * (1.0f - material.floatPack2.g);
      fragColor = vec4(col.rgb, alpha); 
   }
   else if(bufferType == 2)
   {
      // Textured
      // floatPack1 = uvMultiply, uvShift
      // floatPack2 = isAABuffer, 0,0,bufferType
      
      vec4 col = texture(sampler2D(guiTexture, guiSampler), inUV * material.floatPack1.rg + material.floatPack1.ba);
      float alpha = inCol.a * material.floatPack2.r + col.a * (1.0f - material.floatPack2.r);
      fragColor = vec4(col.rgb, alpha) * material.color1; 
   }
   else if(bufferType == 3)
   {
      // Simple Text
      // floatPack1 = 0,0,0,bufferType
      fragColor = vec4(inCol.rgb, texture(sampler2D(guiTexture, guiSampler), inUV).r * inCol.a);
   }
   else if(bufferType == 4)
   {
      // SDF text
      // floatPack1 = thickness, softness, outlineThickness, hasOutline
      // floatPack2 = flipAlpha, 0, 0, bufferType

      float d = texture(sampler2D(guiTexture, guiSampler), inUV).r;
      float alpha = smoothstep(material.floatPack1.r - material.floatPack1.g, material.floatPack1.r + material.floatPack1.g, d);
      float border = smoothstep(material.floatPack1.r + material.floatPack1.b - material.floatPack1.g, material.floatPack1.r + material.floatPack1.b + material.floatPack1.g, d);
      vec3 outlinedColor = mix(material.color1, inCol, border).rgb;
      vec3 baseColor = mix(inCol.rgb, outlinedColor, material.floatPack1.a);
      alpha = (1.0f - alpha) * material.floatPack2.r + alpha * (1.0f - material.floatPack2.r);
      fragColor = vec4(baseColor, alpha * inCol.a);
   }

}

#lina_end
